plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
}

compileJava {
    sourceCompatibility = targetCompatibility = "17"
}

version = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim().replace("/", "-") + "-SNAPSHOT"
group = "net.buildtheearth"

repositories {
    mavenCentral()
    maven { // Required for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { // For bukkit api bindings
        name = "SpigotMC"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {

    // The origins of the dependencies listed here correspond to the project that initially import them in Terra++.
    // If they are needed here, it's because Terra++ ultimately needs them as well.

    // Terra++ dependencies
    implementation "org.apache.commons:commons-imaging:1.0.0-alpha5"
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation("net.daporkchop.lib:binary:0.5.7-SNAPSHOT")  {
        exclude group: "io.netty"
    }

    // Minecraft dependencies
    implementation "it.unimi.dsi:fastutil:8.5.16"
    implementation "com.google.code.gson:gson:2.13.1"
    implementation "com.google.guava:guava:33.4.8-jre"
    implementation "io.netty:netty-all:4.2.3.Final"
    implementation "org.apache.logging.log4j:log4j-api:2.17.1"
    implementation "org.apache.logging.log4j:log4j-core:2.17.1"

    // Tests
    testImplementation "junit:junit:4.13.2"

    // Annotations
    compileOnly "org.projectlombok:lombok:1.18.22"
    annotationProcessor "org.projectlombok:lombok:1.18.22"
    compileOnly 'org.jetbrains:annotations:26.0.2'

    // Bindings to other projects
    compileOnly "org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT"

}

publishing {
    repositories {
        maven {
            name = "smylerSnapshots"
            url = uri("https://maven.smyler.net/snapshots")
            credentials {
                username System.getenv("MAVEN_NAME")
                password System.getenv("MAVEN_SECRET")
            }
        }
    }
    publications {
        terraminusminus(MavenPublication) {
            groupId = project.group
            artifactId = "terraminusminus"
            version = project.version
            from(components["java"])
        }
    }
}