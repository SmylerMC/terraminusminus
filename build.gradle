plugins {
    id "java"
    id "eclipse"
    id "maven-publish"
}

compileJava {
    sourceCompatibility = targetCompatibility = "17"
    options.compilerArgs << '--add-exports=java.desktop/sun.awt.image=ALL-UNNAMED'
}

version = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim().replace("/", "-") + "-SNAPSHOT"
group = "net.buildtheearth"

repositories {
    mavenCentral()
    maven {
        name = "JitPack"
        url = "https://jitpack.io/"
    }
    maven { // Required for leveldb
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven { // Required for leveldb
        name = "OpenCollab Snapshots"
        url = "https://repo.opencollab.dev/snapshot/"
    }
    maven { // Required for LZMA
        name = "Sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven { // For bukkit api bindings
        name = "SpigotMC"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

dependencies {

    // The origins of the dependencies listed here correspond to the project that initially import them in Terra++.
    // If they are needed here, it's because Terra++ ultimately needs them as well.

    // Terra++ dependencies
    implementation "com.github.DaMatrix:commons-imaging:4c6c5dfe6401a884cb4cf53fb838c99e1dfb104c"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.1"
    implementation("net.daporkchop.lib:binary:0.5.7-SNAPSHOT")  {
        exclude group: "io.netty"
    }

    // Minecraft dependencies
    implementation "commons-codec:commons-codec:1.15"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "it.unimi.dsi:fastutil:8.5.6"
    implementation "com.google.code.gson:gson:2.8.9"
    implementation "com.google.guava:guava:31.0.1-jre"
    implementation "io.netty:netty-all:4.1.72.Final"
    implementation "org.apache.logging.log4j:log4j-api:2.17.1"
    implementation "org.apache.logging.log4j:log4j-core:2.17.1"

    // Mixin dependencies
    implementation 'lzma:lzma:0.0.1'

    // Tests
    testImplementation "junit:junit:4.13.2"

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.22"
    annotationProcessor "org.projectlombok:lombok:1.18.22"

    // Bindings to other projects
    compileOnly "org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT"

}

publishing {
    repositories {
        maven {
            name = "smylerSnapshots"
            url = uri("https://maven.smyler.net/snapshots")
            credentials {
                username System.getenv("MAVEN_NAME")
                password System.getenv("MAVEN_SECRET")
            }
        }
    }
    publications {
        terraminusminus(MavenPublication) {
            groupId = project.group
            artifactId = "terraminusminus"
            version = project.version
            from(components["java"])
        }
    }
}